package com.incquerylabs.v4md.test.queries

import "http://www.nomagic.com/magicdraw/UML/2.5.1"

/**
 * Pattern responsible for detecting duplicate ID Property tagged values 
 */
pattern IDStringPropertyEqualityError(elem : NamedElement, property : Property){
	find PropertyStringValueEquals(elem, otherElement, property);
	elem != otherElement;
}

/**
 * Detects NamedElement pairs, where the values of the same STRING ID property are equal
 */
pattern PropertyStringValueEquals(element : NamedElement, element2 : NamedElement, property : Property){
	element != element2;
	Property.isID(property, true);
	NamedElement.appliedStereotypeInstance.slot(element, slot);
	Slot.definingFeature(slot, property);
	
	NamedElement.appliedStereotypeInstance.slot(element2, slot2);
	Slot.definingFeature(slot2, property);
	
	Slot.value(slot, spec1);
	Slot.value(slot2, spec2);
	
	LiteralString.value(spec1, val1);
	LiteralString.value(spec2, val2);
	val1 == val2;
}